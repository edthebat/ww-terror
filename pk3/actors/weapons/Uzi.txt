// Original sprite: Necronixxus.
// Hand: MidoriMan.
// Muzzle Flash: Blood by Monolith.
// Edits and pickup: wildweasel.
// Sounds: Freesound.org, Firearms for Half-Life.

ACTOR UziExp : Ammo
{
	Inventory.MaxAmount 1000
	+ignoreskill
	+undroppable
}

ACTOR UziLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR UziLoaded : Ammo
{
	Tag "9x19mm"
	Inventory.MaxAmount 32
	+IGNORESKILL
}

ACTOR UziSeq : Inventory{}	// And here's how I add half a tic to fire rate.

ACTOR WWTUzi : WWTWeapon replaces Shotgun
{
	Weapon.AmmoType1 "UziLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "9mmAmmo"
	Weapon.AmmoGive2 10
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "$WWTWEP4PICK"
	Inventory.PickupSound "Uzi/Cock"
	Inventory.Icon "UZIPA0"
	Radius 24
	Height 12
	Scale .7
	Tag "$WWTWEP4NAME"
	States
	{
	Spawn:
		UZIP A -1
		Stop
	Select:
		TNT1 A 5 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		UZIZ A 0 A_JumpIfInventory("UziLevelup", 3,3)
		UZIX A 0 A_JumpIfInventory("UziLevelup", 1,2)
		UZIG A 0
		"####" A 1 Offset(90,91)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(18,41)
		Goto ReloadFinishReturn
	Deselect:
		"####" A 1 Offset(3,32) A_StopSound(5)
		"####" A 1 Offset(6,33)
		"####" A 1 Offset(11,35)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(48,61)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(90,91)
		TNT1 A 0 A_Lower
		Wait
	Ready:
		UZIZ A 0 A_JumpIfInventory("UziLevelup",3,3)
		UZIX A 0 A_JumpIfInventory("UziLevelup",1,2)
		UZIG A 0
		"####" A 0 A_JumpIfInventory("UziLoaded",0,2)
		"####" A 0 A_JumpIfInventory("9mmAmmo",1,2)
		"####" A 1 A_WeaponReady
		Loop
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		"####" B 0 A_JumpIfInventory("UziLoaded",1,1)
		Goto DryFire
		"####" B 0 A_GunFlash
		"####" B 0 A_JumpIfInventory("UziLevelup",3,"FireLevel3")
		"####" B 0 A_PlaySound("Uzi/Fire",CHAN_WEAPON)
		"####" B 0 A_JumpIfInventory("UziLevelup",2,"FireLevel2")
		"####" B 0 A_JumpIfInventory("UziLevelup",1,"FireLevel1")
		"####" B 0 A_GunFlash
		"####" B 0 A_AlertMonsters
		"####" B 1 BRIGHT A_FireCustomMissile("PlayerUziTracer",frandom(-1.2,1.2),1,8,0,0,frandom(-1.2,1.2))
		"####" B 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"####" C 1 BRIGHT A_SetPitch(pitch - (.9 * CallACS("RecoilAmount")))
		"####" B 0 A_JumpIfInventory("UziSeq",1,"Fire2")
		"####" A 0 Offset(4,36) A_GiveInventory("UziSeq")
	FireResume:
		"####" A 1 Offset(10,38)
		"####" A 1 Offset(11,39) A_Refire
		"####" A 1 Offset(11,40) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(10,39) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(8,38) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		"####" A 0 A_CheckReload
		Goto Ready
	Fire2:
		"####" A 1 A_TakeInventory("UziSeq")
		Goto FireResume
	FireLevel1:
		"####" B 0 A_AlertMonsters
		"####" B 1 BRIGHT A_FireCustomMissile("PlayerUziTracer",frandom(-1.2,1.2),1,8,0,0,frandom(-1.2,1.2))
		"####" B 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"####" C 1 BRIGHT A_SetPitch(pitch - (.3 * CallACS("RecoilAmount")))
		"####" B 0 A_JumpIfInventory("UziSeq",1,"Fire2Level1")
		"####" A 0 Offset(4,36) A_GiveInventory("UziSeq")
	FireResumeLevel1:
		"####" A 1 Offset(10,38)
		"####" A 1 Offset(11,39) A_Refire
		"####" A 1 Offset(11,40) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(10,39) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(8,38) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(6,36) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(3,34) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(1,33) A_WeaponReady(WRF_NOBOB)
		"####" A 1 Offset(-1,31) A_WeaponReady(WRF_NOBOB)
		"####" A 0 A_CheckReload
		Goto Ready
	Fire2Level1:
		"####" A 1 A_TakeInventory("UziSeq")
		Goto FireResumeLevel1
	FireLevel2:
		"####" B 0 A_AlertMonsters
		"####" B 1 BRIGHT A_FireCustomMissile("PlayerUziTracer",frandom(-1.2,1.2),1,8,0,0,frandom(-1.2,1.2))
		"####" B 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"####" C 1 BRIGHT A_SetPitch(pitch - (.4 * CallACS("RecoilAmount")))
		"####" A 0 Offset(4,36)
		Goto FireResumeLevel1
	FireLevel3:
		"####" B 0 A_PlaySound("Uzi/FireSilent",CHAN_WEAPON)
		"####" B 1 BRIGHT A_FireCustomMissile("PlayerUziTracer",frandom(-0.9,0.9),1,8,0,0,frandom(-0.9,0.9))
		"####" B 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"####" C 1 BRIGHT A_SetPitch(pitch - (.3 * CallACS("RecoilAmount")))
		"####" A 0 Offset(4,36)
		Goto FireResumeLevel1
	Dryfire:
		"####" A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		"####" A 1 A_PlaySound("Dryfire/SMG",6)
		"####" A 1 Offset(2,34)
		"####" A 1 Offset(1,33)
		Goto Ready
	Reload:
		UZIG A 1 A_JumpIfInventory("9mmAmmo", 1, 1)
		Goto Ready
		UZIZ A 0 A_JumpIfInventory("UziLevelup", 3,3)
		UZIX A 0 A_JumpIfInventory("UziLevelup", 1,2)
		UZIG A 0 A_Jump(256,3)
		"####" DE 1
		"####" "#" 1 Offset(1,33) A_PlaySound("Uzi/Clipout",5)
		"####" "#" 1 Offset(1,34)
		"####" "#" 1 Offset(1,35)
		"####" "#" 1 Offset(2,36) A_FireCustomMissile("SpentClipSMG",3,0,5,0)
		"####" "#" 1 Offset(2,37)
		"####" "#" 1 Offset(2,38)
		"####" "#" 1 Offset(3,44)
		"####" "#" 1 Offset(3,48)
		"####" "#" 1 Offset(4,52)
		"####" "#" 1 Offset(4,57)
		"####" "#" 1 Offset(5,62)
		"####" "#" 1 Offset(5,65)
		"####" "#" 1 Offset(6,68)
		"####" "#" 1 Offset(7,70)
		"####" "#" 1 Offset(7,72)
		"####" "#" 1 Offset(8,73)
		"####" "#" 1 Offset(8,74)
		"####" "#" 1 Offset(9,75)
		"####" "#" 1 Offset(10,76)
	ReloadLoop:
		"####" "#" 0 A_TakeInventory("9mmAmmo",1)
		"####" "#" 0 A_GiveInventory("UziLoaded")
		"####" "#" 0 A_JumpIfInventory("UziLoaded",32,"ReloadFinish")
		"####" "#" 0 A_JumpIfInventory("9mmAmmo",1,"ReloadLoop")
		Goto ReloadFinish
	ReloadFinish:
		"####" "#" 1 Offset(10,74)
		"####" "#" 1 Offset(10,72)
		"####" "#" 1 Offset(10,70) A_PlaySound("Uzi/Clipin",5)
		"####" "#" 1 Offset(10,62)
		"####" "#" 1 Offset(10,57)
		"####" "#" 1 Offset(11,52)
		"####" "#" 1 Offset(11,46)
		"####" "#" 1 Offset(11,40)
		"####" "#" 1 Offset(12,30)
		"####" "#" 1 Offset(12,33)
		"####" "#" 1 Offset(13,36)
		"####" "#" 1 Offset(14,39)
		"####" "#" 1 Offset(14,42)
		"####" "#" 1 Offset(14,45)
	ReloadFinishReturn:
		"####" "#" 1 Offset(14,46) A_PlaySound("Uzi/Cock",5)
		UZRZ A 0 A_JumpIfInventory("UziLevelup",3,3)
		UZXR A 0 A_JumpIfInventory("UziLevelup",1,2)
		UZIR A 0
		"####" A 1 Offset(15,47)
		"####" B 1 Offset(16,48)
		"####" C 1 Offset(17,49)
		"####" D 1 Offset(17,50)
		"####" E 1 Offset(16,51)
		"####" F 1 Offset(15,52)
		"####" G 1 Offset(14,53)
		"####" H 1 Offset(12,54)
		"####" I 1 Offset(10,53)
		"####" J 1 Offset(9,50)
		"####" K 1 Offset(8,48)
		UZIZ A 0 A_JumpIfInventory("UziLevelup",3,3)
		UZIX A 0 A_JumpIfInventory("UziLevelup",1,2)
		UZIG A 0
		"####" "#" 1 Offset(6,45)
		"####" "#" 1 Offset(3,41)
		"####" "#" 1 Offset(2,35)
		"####" "#" 1 Offset(1,33)
		"####" "#" 1 Offset(-1,31)
		Goto Ready
	AltfireReturn:
		UZIZ A 0 A_JumpIfInventory("UziLevelup",3,3)
		UZIX A 0 A_JumpIfInventory("UziLevelup",1,2)
		UZIG A 0
		"####" A 1 Offset(-30,82)
		"####" A 1 Offset(-25,72)
		"####" A 1 Offset(-20,62)
		"####" A 1 Offset(-15,52)
		"####" A 1 Offset(-10,44)
		"####" A 1 Offset(-5,38)
		"####" A 1 Offset(-1,35)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}