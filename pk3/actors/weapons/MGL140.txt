// Cooke GLP-140 (Milkor MGL-140)
// Model credits:
// - Ultimate Bastard
// - Felipe Paiva
// - Teh Snake
// - Inph!n!tY
// - TheLama
// - DeadPixel
// - Racer445
// Sound credits:
// - Vabadus
// - Yzhamua
// - Bmaczero
// - Soldier of Fortune 2

ACTOR MGLExp : Ammo
{
	Inventory.MaxAmount 1000
	+ignoreskill
	+undroppable
}

ACTOR MGLLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR MGLLoaded : Ammo
{
	Tag "40mm HE Grenade"
	Inventory.MaxAmount 6
	+IGNORESKILL
}

ACTOR MGL140 : WWTWeapon replaces PlasmaRifle
{
	Scale .7
	Weapon.AmmoType1 "MGLLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "40mmAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 6
	Inventory.Icon "MGLPA0"
	Inventory.PickupSound "MGL/Close"
	Inventory.PickupMessage "$WWTWEP7PICK"
	Tag "$WWTWEP7NAME"
	States
	{
	Spawn:
		MGLP A -1
		Stop
	Select:
		TNT1 A 15 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
		MGLG A 1 Offset(90,91)
		MGLG A 1 Offset(75,81)
		MGLG A 1 Offset(60,71)
		MGLG A 1 Offset(48,61) A_PlaySound("MGL/Close",5)
		MGLG A 1 Offset(38,51)
		MGLG A 1 Offset(29,43)
		MGLG A 1 Offset(20,36)
		MGLG A 1 Offset(11,35)
		MGLG A 1 Offset(6,33)
		MGLG A 1 Offset(3,32)
		Goto Ready
	Deselect:
		MGLG A 1 Offset(3,32) A_StopSound(5)
		MGLG A 1 Offset(6,33)
		MGLG A 1 Offset(11,35)
		MGLG A 1 Offset(20,36)
		MGLG A 1 Offset(29,43)
		MGLG A 1 Offset(38,51)
		MGLG A 1 Offset(48,61)
		MGLG A 1 Offset(60,71)
		MGLG A 1 Offset(75,81)
		MGLG A 1 Offset(90,91)
		TNT1 A 0 A_Lower
		Wait
	Ready:
		MGLG A 0 A_JumpIfInventory("MGLLoaded",0,2)
		MGLG A 0 A_JumpIfInventory("40mmAmmo",1,2)
		MGLG A 1 A_WeaponReady
		Loop
		MGLG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		MGLG A 0 A_JumpIfInventory("MGLLoaded",1,1)
		Goto DryFire
		MGLG A 0 A_PlaySound("MGL/Fire",CHAN_WEAPON)
		MGLG A 0 A_JumpIfInventory("MGLLevelUp",3,"FireLevel3")
		MGLG A 0 A_JumpIfInventory("MGLLevelUp",2,"FireLevel2")
		MGLG A 0 A_JumpIfInventory("MGLLevelUp",1,"FireLevel1")
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerLauncherGrenade",0,1,8)
	FireContinue:
		MGLG B 1 Bright Offset(6,38) A_SetPitch(pitch - (1.5 * CallACS("RecoilAmount")))
		MGLG C 1 Bright Offset(9,42) A_SetPitch(pitch - (1.2 * CallACS("RecoilAmount")))
		MGLG A 1 Bright Offset(12,46) A_SetPitch(pitch - (.9 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(13,48) A_SetPitch(pitch - (.6 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(12,45) A_SetPitch(pitch - (.3 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(10,42) A_SetPitch(pitch + (.1 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(8,39) A_SetPitch(pitch + (.2 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(6,36) A_SetPitch(pitch + (.3 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(3,34) A_SetPitch(pitch + (.4 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(1,33) A_SetPitch(pitch + (.5 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(-1,31) A_SetPitch(pitch + (.3 * CallACS("RecoilAmount")))
		MGLG A 1 Offset(-1,32) A_SetPitch(pitch + (.1 * CallACS("RecoilAmount")))
		MGLG A 0 A_CheckReload
		Goto Ready
	FireLevel1:
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerLauncherGrenadeFaster",0,1,8)
		Goto FireContinue
	FireLevel2:
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerLauncherGrenadeFaster2",0,1,8)
		Goto FireContinue
	FireLevel3:
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("PlayerLauncherGrenadeFinal",0,1,8)
		Goto FireContinue
	Dryfire:
		TNT1 A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		MGLG A 1 A_PlaySound("Dryfire/Heavy",6)
		MGLG A 1 Offset(2,34)
		MGLG A 1 Offset(1,33)
		Goto Ready
	Reload:
		MGLG A 1 A_JumpIfInventory("40mmAmmo",1,1)
		Goto Ready
		MGLG A 1 A_PlaySound("MGL/Open",5)
		MGLG A 1 Offset(1,35)
		MGLG A 1 Offset(2,38)
		MGLG A 1 Offset(3,41)
		MGLG A 1 Offset(4,44)
		MGLG A 1 Offset(5,47)
		MGLG A 1 Offset(6,50)
		MGLG A 1 Offset(7,52)
		MGLG A 1 Offset(8,54)
		MGLG A 1 Offset(9,56)
		MGLG A 1 Offset(10,57)
		MGLG A 1 Offset(11,58)
		MGLG A 1 Offset(13,59)
		MGLG A 1 Offset(14,60)
	ReloadWork:
		MGLG A 0 A_TakeInventory("40mmAmmo",1)
		MGLG A 0 A_GiveInventory("MGLLoaded")
		MGLG A 1 Offset(12,65) A_PlaySound("MGL/Load",5)
		MGLG A 1 Offset(11,66) A_SpawnItemEx("40mmCasing",24,-12,24,0,0,0,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MGLG A 1 Offset(10,67) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(9,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(8,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(7,70) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(8,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(9,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(12,67) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(13,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(14,71) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(15,72) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(16,73) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(17,74) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(18,75) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(19,76) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(18,75) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(17,74) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(16,73) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(15,72) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(14,70) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(13,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(12,66) A_WeaponReady(WRF_NOBOB)
		MGLG A 0 A_JumpIfInventory("MGLLoaded",6,2)
		MGLG A 0 A_JumpIfInventory("40mmAmmo",1,"ReloadWork")
	ReloadFinish:
		MGLG A 1 Offset(11,57) A_PlaySound("MGL/Close",5)
		MGLG A 1 Offset(9,50)
		MGLG A 1 Offset(5,45)
		MGLG A 1 Offset(2,39)
		MGLG A 1 Offset(1,35)
		Goto Ready
	AltfireReturn:
		MGLG A 1 Offset(-30,82)
		MGLG A 1 Offset(-25,72)
		MGLG A 1 Offset(-20,62)
		MGLG A 1 Offset(-15,52)
		MGLG A 1 Offset(-10,44)
		MGLG A 1 Offset(-5,38)
		MGLG A 1 Offset(-1,35)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}