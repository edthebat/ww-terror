ACTOR GrenadeExp : Ammo
{
	Inventory.MaxAmount 1000
	+ignoreskill
	+undroppable
}

ACTOR GrenadeLevelup : Inventory
{
	Inventory.MaxAmount 3
}

ACTOR GrenadePickup : CustomInventory replaces RocketAmmo
{
	inventory.pickupmessage "Picked up a grenade."
	Tag "Grenade"
	inventory.maxamount 9
	inventory.icon TNT1A0
	inventory.usesound "grenade/throw"
	inventory.pickupsound "grenade/pickup"
	+Inventory.invbar
	+inventory.keepdepleted
	Inventory.InterHubamount 9
	Radius 16
	Height 8
	Scale .25
	states
	{
	Spawn:
		THRW A -1
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup",3,"Use3")
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup",2,"Use2")
		TNT1 A 0 A_JumpIfInventory("GrenadeLevelup",1,"Use1")
		TNT1 A 0 A_FireCustomMissile("PipebombGrenade",0,0,0,0,FPF_NOAUTOAIM)
		Stop
	Use1:
		TNT1 A 0 A_FireCustomMissile("HandGrenade",0,0,0,0,FPF_NOAUTOAIM)
		stop
	Use2:
		TNT1 A 0 A_FireCustomMissile("ElectricGrenade",0,0,0,0,FPF_NOAUTOAIM)
		Stop
	Use3:
		TNT1 A 0 A_FireCustomMissile("IncendiaryGrenade",0,0,0,0,FPF_NOAUTOAIM)
		Stop
	}
}

ACTOR PipebombGrenade
{
	+HITTRACER
	Radius 4
	Height 3
	Speed 40
	Scale .25
	Damage (1)
	DamageType "PlayerGrenade"
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+CANBOUNCEWATER
	obituary "%o went down the pipes, thanks to %k's pipebomb."
	BounceType Doom
	BounceFactor .4
	WallBounceFactor .4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		PIPE A 2 Bright A_Countdown
		PIPE BCD 2 Bright
		loop
	Death:
		PIPE A 35 Bright A_GiveInventory("KillCheckGrenade",1,AAPTR_TRACER)
		FRME A 0 A_SetDamageType("PlayerGrenadeBoom")
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 0 A_Explode(150,256)
		FRME A 1 bright A_RadiusGive("KillCheckGrenade",256,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR KillCheckGrenade : KillCheck
{
	States
	{
	Win:
		TNT1 A 0 A_GiveToTarget("GrenadeExp")
		Stop
	}
}

ACTOR HandGrenade : PipebombGrenade
{
	Speed 30
	Gravity .75
	obituary "%o failed to field %k's fragmenting fastball."
	states
	{
	Spawn:
		THRW A 2 Bright A_Countdown
		THRW BCDEF 2 Bright
		loop
	Death:
		THRW B 35 Bright A_GiveInventory("KillCheckGrenade",1,AAPTR_TRACER)
		FRME A 0 A_SetDamageType("PlayerGrenadeBoom")
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 0 A_Explode(175,256,1,1,128,15,2)
		FRME A 1 bright A_RadiusGive("KillCheckGrenade",256,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR ElectricGrenade : HandGrenade
{
	Scale .55
	DamageType "PlayerElectricGrenade"
	PainType PlayerTaser
	obituary "%o would make a good conductor, says %k."
	states
	{
	Spawn:
		EGRN A 2 Bright A_Countdown
		EGRN BCDEFGH 2 Bright
		loop
	Death:
		EGRN C 35 Bright A_GiveInventory("KillCheckGrenade",1,AAPTR_TRACER)
		FRME A 0 A_SetDamageType("PlayerElectricGrenadeBoom")
		STFL A 0 A_AlertMonsters
		STFL A 0 A_SetTranslucent(0.75,1)
		STFL A 0 A_PlaySound("grenade/elecexplode",0,1.0,0,0.1)
		STFL A 0 A_Quake(7,10,0,512,0)
		STFL A 0 A_Explode(8,192)
		STFL A 1 Bright A_RadiusGive("KillCheckGrenade",192,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL B 1 Bright A_SetScale(0.6)
		STFL C 0 A_Explode(8,200)
		STFL C 1 Bright A_RadiusGive("KillCheckGrenade",200,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL D 1 Bright A_SetScale(0.7)
		STFL E 0 A_Explode(8,208)
		STFL E 1 Bright A_RadiusGive("KillCheckGrenade",208,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL F 1 Bright A_SetScale(0.8)
		STFL G 0 A_Explode(8,216)
		STFL G 1 Bright A_RadiusGive("KillCheckGrenade",216,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL H 1 Bright A_SetScale(0.9)
		STFL I 0 A_Explode(8,224)
		STFL I 1 Bright A_RadiusGive("KillCheckGrenade",224,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL J 1 Bright A_SetScale(1.0)
		STFL K 0 A_Explode(8,232)
		STFL K 1 Bright A_RadiusGive("KillCheckGrenade",232,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL L 1 Bright A_SetScale(1.2)
		STFL M 0 A_Explode(8,240)
		STFL M 1 Bright A_RadiusGive("KillCheckGrenade",240,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL N 1 Bright A_SetScale(1.4)
		STFL O 0 A_Explode(8,248)
		STFL O 1 Bright A_RadiusGive("KillCheckGrenade",248,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL P 1 Bright A_SetScale(1.6)
		STFL A 0 A_Explode(8,256)
		STFL A 1 Bright A_RadiusGive("KillCheckGrenade",256,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL B 1 Bright A_SetScale(1.8)
		STFL C 0 A_Explode(8,264)
		STFL C 1 Bright A_RadiusGive("KillCheckGrenade",264,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL D 1 Bright A_SetScale(2.0)
		STFL E 0 A_Explode(8,272)
		STFL E 1 Bright A_RadiusGive("KillCheckGrenade",272,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL F 1 Bright A_SetScale(2.25)
		STFL G 0 A_Explode(8,280)
		STFL G 1 Bright A_RadiusGive("KillCheckGrenade",280,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL H 1 Bright A_SetScale(2.5)
		STFL I 0 A_Explode(8,288)
		STFL I 1 Bright A_RadiusGive("KillCheckGrenade",288,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL J 1 Bright A_SetScale(2.75)
		STFL K 0 A_Explode(8,296)
		STFL K 1 Bright A_RadiusGive("KillCheckGrenade",296,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL L 1 Bright A_SetScale(3.0)
		STFL M 0 A_Explode(8,304)
		STFL M 1 Bright A_RadiusGive("KillCheckGrenade",304,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL N 1 Bright A_SetScale(3.25)
		STFL O 0 A_Explode(8,312)
		STFL O 1 Bright A_RadiusGive("KillCheckGrenade",312,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		STFL P 1 Bright A_SetScale(3.5)
		stop
	}
}

ACTOR IncendiaryGrenade : PipebombGrenade
{
	DamageType "Fire"
	obituary "%o was roasted, toasted, and burnt to a crisp by %k."
	states
	{
	Spawn:
		GCAN A 1 Bright A_Countdown
		GCAN BCDEFGH 1 Bright
		loop
	Death:
		GCAN D 35 Bright A_GiveInventory("KillCheckGrenade",1,AAPTR_TRACER)
		FBLX A 0 BRIGHT A_SetScale(1.0,1.0)
     	FBLX A 0 BRIGHT A_AlertMonsters
		FBLX A 0 BRIGHT A_SetTranslucent(0.75,1)
		FBLX A 0 BRIGHT A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FBLX A 0 A_Explode(128,192)
		FRME A 1 BRIGHT A_RadiusGive("KillCheckGrenade",192,RGF_MONSTERS|RGF_CORPSES|RGF_CUBE)
		FBLX B 1 BRIGHT A_Quake(7,10,0,256,0)
		FBLX CCCCCCCCCC 0 BRIGHT A_CustomMissile("ThermaBurstMolotovChunk",0,0,(random(0,359)),6,(random(4,15)))
		FBLX CDEFGHIJK 2 BRIGHT
     	Stop
	}
}

ACTOR EnemyGrenade : HandGrenade
{
	DamageType "EnemyGrenade"
	States
	{
	Death:
		THRW B 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(40,128)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR EnemyPipebomb : PipebombGrenade
{
	DamageType "EnemyGrenade"
	States
	{
	Death:
		PIPE A 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(40,128)
		FRME B 1 Bright A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}