ACTOR RepelShock // Borrowed from LWM's Hammerhound.
{
	Speed 8
	Radius 12
	Height 16
	RenderStyle Add
	Alpha .5
	Scale .05
	Projectile
	+DROPOFF
	+NOGRAVITY
	+FLOORHUGGER
	States
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOP 2 A_SetScale(scalex+.05)
	Death:
		SMOK Q 2
		Stop
	}
}

ACTOR DIExplosion
{
	//RenderStyle Add
	Radius 1
	Height 1
	Scale 1.5
	SeeSound "weapons/rocklx"
	Projectile
	+EXPLODEONWATER
	+NOCLIP
	Decal BigScorch
	DamageType Fire
	States
	{
	Spawn:
		EXP1 A 2 BRIGHT //A_Explode(56,160,0)
		EXP1 B 2 BRIGHT Radius_Quake(3,10,0,8,0)
		EXP1 C 2 BRIGHT A_PlaySound("weapons/rocklx",5)
		EXP1 DEFGHIJKLMN 2 BRIGHT
		Stop
	}
}

ACTOR DIExplosionLarge : DIExplosion
{
	Scale 2.5
}

ACTOR SparkSpawner
{
	RenderStyle Add
	Scale .3
	SeeSound "Bullet/HitMetal"
	States
	{
	Death:
	Crash:
	Spawn:
		PUFF A 2 BRIGHT NODELAY A_SpawnDebris("BulletIneffectiveSpark")
		PUFF B 2 BRIGHT A_SetTranslucent(0.9,0)
		PUFF CD 2 BRIGHT
		Stop
	}
}

ACTOR BulletIneffectiveSpark : TracerSpark
{
	States
	{
	Spawn:
		WPUF E 1 BRIGHT A_Fadeout(0.05)
		Loop
	}
}

// Fire actors, from Reelism courtesy of Kinsie et al
ACTOR Fire
{
	Height 10
	Radius 10
	Speed 8
	+NOGRAVITY
	+MISSILE
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("FlameProj1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		TNT1 A 1 A_SpawnItemEx("FlameProj1B")
		TNT1 A 1 A_SpawnItemEx("FlameProj2A")
		TNT1 A 1 A_SpawnItemEx("FlameProj2B")
		TNT1 A 1 A_SpawnItemEx("FlameProj3A")
		TNT1 A 1 A_SpawnItemEx("FlameProj3B")
		TNT1 A 1 A_SpawnItemEx("FlameProj1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		TNT1 A 1 A_SpawnItemEx("FlameProj1B")
		TNT1 A 1 A_SpawnItemEx("FlameProj2A")
		TNT1 A 1 A_SpawnItemEx("FlameProj2B")
		TNT1 A 1 A_SpawnItemEx("FlameProj3A")
		TNT1 A 1 A_SpawnItemEx("FlameProj3B")
		TNT1 A 1 A_SpawnItemEx("FlameProj1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		TNT1 A 1 A_SpawnItemEx("FlameProj1B")
		TNT1 A 1 A_SpawnItemEx("FlameProj2A")
		TNT1 A 1 A_SpawnItemEx("FlameProj2B")
		TNT1 A 1 A_SpawnItemEx("FlameProj3A")
		TNT1 A 1 A_SpawnItemEx("FlameProj3B")
		TNT1 A 1 A_SpawnItemEx("FlameProj1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		TNT1 A 1 A_SpawnItemEx("FlameProj1B")
		TNT1 A 1 A_SpawnItemEx("FlameProj2A")
		TNT1 A 1 A_SpawnItemEx("FlameProj2B")
		TNT1 A 1 A_SpawnItemEx("FlameProj3A")
		TNT1 A 1 A_SpawnItemEx("FlameProj3B")
		TNT1 A 1 A_SpawnItemEx("FlameProj1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		TNT1 A 1 A_SpawnItemEx("FlameProj1B")
		TNT1 A 1 A_SpawnItemEx("FlameProj2A")
		TNT1 A 1 A_SpawnItemEx("FlameProj2B")
		TNT1 A 1 A_SpawnItemEx("FlameProj3A")
		TNT1 A 1 A_SpawnItemEx("FlameProj3B")
		stop
	Death:
		TNT1 A 1 A_SpawnItem("FireImpact")
		Stop
	}
}

ACTOR FlameProj1A
{
	Height 2
	Radius 1
	+NOGRAVITY
	+NOBLOCKMAP
	+MISSILE
	//+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha .8
	Scale .1
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,Random(2,6),0,0)
		"####" A 0 ThrustThing(Random(0,255),Random(0,1),0,0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

ACTOR FlameProj1B : FlameProj1A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

ACTOR FlameProj2A : FlameProj1A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

ACTOR FlameProj2B : FlameProj1A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

ACTOR FlameProj3A : FlameProj1A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

ACTOR FlameProj3B : FlameProj1A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

ACTOR FireImpact
{
	Height 10
	Radius 10
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Explode(10,32)
		TNT1 A 0 A_PlaySound("SFX/FireLoop1",6,1,1)
		TNT1 A 0 A_Jump(216,3)
		TNT1 A 1 A_SpawnItemEx("Flame1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		Loop
		TNT1 A 0 A_Jump(176,2)
		TNT1 A 1 A_SpawnItemEx("Flame1B")
		Loop
		TNT1 A 0 A_Jump(136,2)
		TNT1 A 1 A_SpawnItemEx("Flame2A")
		Loop
		TNT1 A 0 A_Jump(96,2)
		TNT1 A 1 A_SpawnItemEx("Flame2B")
		Loop
		TNT1 A 0 A_Jump(56,2)
		TNT1 A 1 A_SpawnItemEx("Flame3A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3B")
		stop
	}
}

//Flames and particles
ACTOR Flame1A : FlameProj1A
{
	Scale .35
}
ACTOR Flame1B : FlameProj1B
{
	Scale .35
}
ACTOR Flame2A : FlameProj2A
{
	Scale .35
}
ACTOR Flame2B : FlameProj2B
{
	Scale .35
}
ACTOR Flame3A : FlameProj3A
{
	Scale .35
}
ACTOR Flame3B : FlameProj3B
{
	Scale .35
}

ACTOR Flame4A : Flame1A
{
	+NoInteraction
	Scale .7
	States
	{
	Spawn:
		FLM1 A 0 NODELAY ThrustThingZ(0,Random(4,12),0,0)
		FLM1 A 0 ThrustThing(Random(0,255),Random(0,2),0,0)
		Goto Flame1A::Spawn+3
	}
}

ACTOR Flame4B : Flame1B
{
	+NoInteraction
	Scale .7
}

ACTOR Flame5A : Flame2A
{
	+NoInteraction
	Scale .7
}

ACTOR Flame5B : Flame2B
{
	+NoInteraction
	Scale .7
}

ACTOR Flame6A : Flame3A
{
	+NoInteraction
	Scale .7
}

ACTOR Flame6B : Flame3B
{
	+NoInteraction
	Scale .7
}

ACTOR Flame7A : Flame4A
{
	Scale 1.4
	States
	{
	Spawn:
		FLM1 A 0 NODELAY ThrustThingZ(0,Random(8,24),0,0)
		FLM1 A 0 ThrustThing(Random(0,255),Random(0,4),0,0)
		Goto Flame1A::Spawn+3
	}
}

ACTOR Flame7B : Flame4B
{
	Scale 1.4
}

ACTOR Flame8A : Flame5A
{
	Scale 1.4
}

ACTOR Flame8B : Flame5B
{
	Scale 1.4
}

ACTOR Flame9A : Flame6A
{
	Scale 1.4
}

ACTOR Flame9B : Flame6B
{
	Scale 1.4
}

ACTOR Ember1 : Flame1A
{
	Scale .1
	States
	{
	Spawn:
		EMBR A 0 NODELAY ThrustThingZ(0,Random(4,16),0,0)
		EMBR A 0 ThrustThing(Random(0,255),Random(0,1),0,0)
		EMBR A 32 Bright
	Death:
		EMBR A 2 Bright A_FadeOut(0.1)
		Loop
	}
}

ACTOR Ember2 : Ember1
{
	States
	{
	Spawn:
		EMBR A 0 NODELAY ThrustThingZ(0,Random(4,16),0,0)
		EMBR A 0 ThrustThing(Random(0,255),Random(0,1),0,0)
		EMBR A 64 Bright
		Goto Death
	}
}

ACTOR HalpSign
{
	Scale .15
	RenderStyle Translucent
	+NOGRAVITY
	+NOINTERACTION
	States
	{
	Spawn:
		HALP A 1 Bright
		HALP A 1 Bright A_FadeOut(0.1,1)
		Loop
	}
}

// The taser stuff, borrowed (in some instances) from Psychic with
// contributions by Kate

ACTOR TaserPuff
{
	+HITTRACER
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+THRUGHOST
	+NOEXTREMEDEATH
	+BLOODLESSIMPACT
	+PUFFGETSOWNER
	attacksound "Taser/wall"
	seesound "Taser/hit"
	Obituary "%o was a victim of %k's police brutality."
	DamageType "PlayerTaser"
	Scale .3
	states
	{
	Melee:
	Crash:
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_GiveInventory("KillCheckTaser",1,AAPTR_TRACER)
	Finish:
		ZAP1 AABCD 2 BRIGHT
		ZAP1 E 2 BRIGHT A_SetTranslucent(0.40,1)
		ZAP1 F 2 BRIGHT A_SetTranslucent(0.55,1)
		ZAP1 E 2 BRIGHT A_SetTranslucent(0.30,1)
		ZAP1 D 2 BRIGHT A_SetTranslucent(0.10,1)
		Stop

	}
}

ACTOR KillCheckTaser : KillCheck
{
	States
	{
	Win:
		TNT1 A 0 A_GiveToTarget("TaserExp")
		Stop
	}
}

ACTOR TaserFirePuff : TaserPuff
{
	DeathType Fire
}
ACTOR TaserChainPuff : TaserFirePuff
{
	var int user_theta;
	Scale .5
	states
	{
	Melee:
	Crash:
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_GiveInventory("KillCheckTaser",1,AAPTR_TRACER)
	Theta:
		TESL E 0 A_CustomMissile("TeslaTracer1A",0,0,user_theta,CMF_AIMDIRECTION|CMF_TRACKOWNER)
		TESL E 0 A_SetUserVar("user_theta",user_theta + 20)
		TESL E 0 A_JumpIf(user_theta >= 360,"Finish")
		Loop
	}
}

//"Psychic" Decorate,by Xaser the Insane
// Psychic Powers: Lightning

ACTOR TeslaTracer1A : FastProjectile
{
	+HITTRACER
	Height 8
	Radius 6
	Speed 100
	RenderStyle Add
	Alpha .8
	PROJECTILE
	Damagetype "PlayerTaser"
	DeathType Fire
	Obituary "%o couldn't handle %k's shocking revelation."
	var int user_theta;
	Damage 2
	+THRUACTORS
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Death:
		TNT1 A 0 A_GiveInventory("KillCheckTaser",1,AAPTR_TRACER)
	Finish:
		ZAP1 AABCD 2 BRIGHT
		ZAP1 E 2 BRIGHT A_SetTranslucent(0.40,1)
		ZAP1 F 2 BRIGHT A_SetTranslucent(0.55,1)
		ZAP1 E 2 BRIGHT A_SetTranslucent(0.30,1)
		ZAP1 D 2 BRIGHT A_SetTranslucent(0.10,1)
		Stop
	XDeath:
		TNT1 A 0 A_GiveInventory("KillCheckTaser",1,AAPTR_TRACER)
	Theta:
		TESL E 0 A_CustomMissile("TeslaTracer1A",0,0,user_theta,CMF_AIMDIRECTION | CMF_TRACKOWNER)
		TESL E 0 A_SetUserVar("user_theta",user_theta + 20)
		TESL E 0 A_JumpIf(user_theta >= 360,"Finish")
		Loop
	}
}