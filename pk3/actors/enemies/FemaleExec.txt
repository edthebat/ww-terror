ACTOR FemaleExec : WWTBadGuy
{
	XScale 0.4
	YScale 0.35
	Health 100
	Speed 6
	SeeSound "Sights/Female03"
	PainSound "Pains/Female03"
	DeathSound "Deaths/Female03"
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.3
	DamageFactor "PlayerPistol",0.4
	DamageFactor "PlayerUzi",0.4
	DamageFactor "PlayerShotgun",0.4
	DamageFactor "PlayerAR",0.9
	DamageFactor "PlayerM60",0.9
	DamageFactor "PlayerMagnum",0.7
	DamageFactor "PlayerMelee",2.0
	PainChance "PlayerPistol",32
	PainChance "PlayerUzi",32
	PainChance "PlayerShotgun",32
	Obituary "%o can't see shit, Captain."
	States
	{
	Spawn:
		FXEC A 0 NODELAY A_GiveInventory("ResistBeretta")
		FXEC A 0 A_GiveInventory("ResistUzi")
		FXEC A 1 A_GiveInventory("ResistStriker")
		FXEC A 1 A_Look
		Wait
	See:
		FXEC A 0 A_GiveInventory("ResistBeretta")
		FXEC A 0 A_GiveInventory("ResistUzi")
		FXEC A 0 A_GiveInventory("ResistStriker")
		FXEC B 1 A_Chase
		FXEC BB 1 A_Chase("","")
		FXEC C 1 A_Chase
		FXEC CC 1 A_Chase("","")
		FXEC D 1 A_Chase
		FXEC DD 1 A_Chase("","")
		FXEC E 1 A_Chase
		FXEC EE 1 A_Chase("","")
		Loop
	Missile:
		FXEC FG 4 A_FaceTarget
		FXEC H 0 A_PlaySound("Flash/fire",CHAN_WEAPON)
		FXEC H 8 Bright A_CustomMissile("FlashBullet",46)
		FXEC I 8
		FXEC GF 4
		Goto See
	Pain.PlayerTaser:
		FXEC J 0 A_PlaySound("Tasered/Female",CHAN_VOICE)
		Goto Tased+1
	Pain:
		FXEC J 3 A_Pain
		Goto See
	Death:
		FXEC K 3 A_Scream
		FXEC LM 3
		FXEC N 3 A_PlaySound("Misc/Bodyfall")
		FXEC O 3 A_NoBlocking
		FXEC PQR 3
		FXEC S -1
		Stop
	Death.Fire:
		"####" B 0 A_PlaySound("Burning/Female")
		Goto Super::Death.Fire+1
	}
}

ACTOR FlashBullet
{
	Radius 5
	Height 8
	Speed 30
	Damage (8)
	PROJECTILE
	SeeSound "Flash/Fire"
	DamageType "EnemyFlash"
	States
	{
	Spawn:
		ARWP AA 0 A_SpawnItemEx("BulletIneffectiveSpark",0,0,0,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(3.0,6.0),frandom(0.0,359.9),SXF_NOCHECKPOSITION)
		ARWP A 1 Bright
		Loop
	Death:
		Stop
	}
}