ACTOR SilentJ : WWTBadGuy
{
	Scale 0.35
	Health 50
	SeeSound "Sights/Male01"
	PainSound "Pains/Male01"
	DeathSound "Deaths/Male01"
	DropItem "9mmAmmoSmall"
	DamageFactor "EnemyBullet",0.3
	DamageFactor "EnemyGrenade",0.3
	DamageFactor "PlayerPistol",0.5
	DamageFactor "PlayerUzi",0.5
	DamageFactor "PlayerMelee",2.0
	PainChance "PlayerPistol",32
	PainChance "PlayerUzi",32
	Obituary "%o got brought down all silent-like by Silent J."
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		ORAN A 0 NODELAY A_GiveInventory("ResistBeretta")
		ORAN A 1 A_GiveInventory("ResistUzi")
		ORAN A 1 A_Look
		Wait
	See:
		ORAN A 0 A_GiveInventory("ResistBeretta")
		ORAN A 0 A_GiveInventory("ResistUzi")
		Goto Super::See
	Missile:
		ORAN F 10 A_FaceTarget
		ORAN G 0 A_PlaySound("Beretta/Silent",CHAN_WEAPON)
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(55,80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ORAN G 2 BRIGHT A_CustomMissile("EnemyARTracer",52,0,frandom(-2.0,2.0))
		ORAN F 6
		ORAN G 0 A_PlaySound("Beretta/Silent",CHAN_WEAPON)
		COP3 G 0 A_SpawnItemEx("BulletCasing",24,-24,40,8,2,4,random(55,80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		ORAN G 2 BRIGHT A_CustomMissile("EnemyARTracer",52,0,frandom(-2.0,2.0))
		ORAN F 2
		ORAN H 5
		ORAN F 4
		Goto See
	Pain.PlayerTaser:
		ORAN I 0
		Goto Tased
	Pain:
		ORAN I 3 A_Pain
		Goto See
	Death:
		ORAN J 3 A_Scream
		ORAN KL 3
		ORAN M 3 A_PlaySound("Misc/Bodyfall")
		ORAN N 3 A_NoBlocking
		ORAN OPQ 3
		ORAN R -1
		Stop
	}
}